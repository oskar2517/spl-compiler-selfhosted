#define AND(a, b) ((a) * (b))
#define OR(a, b)  ((a) + (b) - (a) * (b))
#define COND(c, r) if (c) r := 1; else r := 0;

#define LINE_FFED 10

#define TOK_IDENT    0
#define TOK_INT      1
#define TOK_CHAR     2
#define TOK_IF       3
#define TOK_ELSE     4
#define TOK_WHILE    5
#define TOK_ARRAY    6
#define TOK_OF       7
#define TOK_PROC     8
#define TOK_REF      9
#define TOK_TYPE     10
#define TOK_VAR      11
#define TOK_LPAREN   12
#define TOK_RPAREN   13
#define TOK_LBRACK   14
#define TOK_RBRACK   15
#define TOK_LCURL    16
#define TOK_RCURL    17
#define TOK_EQUAL    18
#define TOK_HASH     19
#define TOK_LT       20
#define TOK_LTE      21
#define TOK_GT       22
#define TOK_GTE      23
#define TOK_PLUS     24
#define TOK_MINUS    25
#define TOK_ASTERISK 26
#define TOK_SLASH    27
#define TOK_COLON    28
#define TOK_SEMIC    29
#define TOK_COMMA    30
#define TOK_ASSGN    31
#define TOK_EOF      32
#define TOK_ILLEGAL  33

#define NEXT_CHAR charPointer := charPointer + 1;
#define TOKEN(type) tokens[tokenPointer] := type; tokenPointer := tokenPointer + 1;
#define K_MATCH(i, c) if (input[charPointer + (i)] = c)

#define CHAR_TO_INT(c) ((c) - 0x30)

type DataArray = array[99999] of int;

proc array_cpy(ref source: DataArray, ref target: DataArray, start: int, end: int, startOffset: int) {
    var i: int;

    while (i < (end - start)) {
        target[i + startOffset] := source[start + i];

        i := i + 1;
    }
}

proc read_input(ref input: DataArray, ref length: int) {
    var char: int;
    var index: int;

    readc(char);
    input[0] := char;

    index := 1;
    while (char # 0x2E) { // Input file must end with dot (0x2E) to indicate EOF
        readc(char);
        input[index] := char;
        index := index + 1;
    }

    // Replace dot (0x2E) with 0 to indicate EOF
    input[index - 1] := 0;
    length := index - 1;
}

proc lex_is_whitespace(c: int, ref r: int) {
    var space: int;
    var lineFeed: int;
    var carriageReturn: int;
    var horizontalTab: int;
    
    COND(c = 32, space)
    COND(c = LINE_FFED, lineFeed)
    COND(c = 13, carriageReturn)
    COND(c = 9, horizontalTab)

    r := OR(OR(OR(space, lineFeed), carriageReturn), horizontalTab);
}

proc lex_is_digit(c: int, ref r: int) {
    r := 0;

    if (c >= 0x30) {
        if (c <= 0x39) {
            r := 1;
        } 
    }
}

proc lex_is_alphanumeric_or_underscore(c: int, ref r: int) {
    var isUpperCaseLetter: int;
    var isLowerCaseLetter: int;
    var isDigit: int;
    var isUnderscore: int;

    isUpperCaseLetter := 0;
    isLowerCaseLetter := 0;
    isDigit := 0;
    isUnderscore := 0;

    if (c >= 0x41) {
        if (c <= 0x5A) {
            isUpperCaseLetter := 1;
        }
    }
    
    if (c >= 0x61) {
        if (c <= 0x7A) {
            isLowerCaseLetter := 1;
        }
    }

    if (c = '_') {
        isUnderscore := 1;
    }

    lex_is_digit(c, isDigit);

    r := OR(OR(OR(isUpperCaseLetter, isLowerCaseLetter), isDigit), isUnderscore);
}

proc lex_is_hex(c: int, ref r: int) {
    var t: int;

    lex_hex_to_int(c, r);

    COND(r # -1, r);
}

proc lex_hex_to_int(c: int, ref r: int) {
    r := -1;

    if (c >= 'a') {
        if (c <= 'f') {
            r := c - 87;
        }
    }

    if (c >= 'A') {
        if (c <= 'F') {
            r := c - 55;
        }
    }

    if (c >= '0') {
        if (c <= '9') {
            r := CHAR_TO_INT(c);
        } 
    }
}

proc lex_eat_whitespace(ref input: DataArray, ref charPointer: int) {
    var isWhitespace: int;

    lex_is_whitespace(input[charPointer], isWhitespace);

    while (isWhitespace # 0) {
        NEXT_CHAR
        lex_is_whitespace(input[charPointer], isWhitespace);
    }
}

proc lex_eat_comment(ref input: DataArray, ref charPointer: int) {
    var isCurrentCharSlash: int;
    var isPeekCharSlash: int;
    var isNotLineFeed: int;
    var isEof: int;

    COND(input[charPointer] = '/', isCurrentCharSlash)
    COND(input[charPointer + 1] = '/', isPeekCharSlash)

    if (AND(isCurrentCharSlash, isPeekCharSlash) # 0) {
        COND(input[charPointer] # LINE_FFED, isNotLineFeed)
        COND(input[charPointer] # 0, isEof)

        while (AND(isNotLineFeed, isEof) # 0) {
            NEXT_CHAR

            COND(input[charPointer] # LINE_FFED, isNotLineFeed)
            COND(input[charPointer] # 0, isEof)
        }

        lex_eat_whitespace(input, charPointer);
        lex_eat_comment(input, charPointer);
    }
}

proc lex_read_ident(ref input: DataArray, ref charPointer: int, ref tokens: DataArray, ref tokenPointer: int) {
    var startIndex: int;
    var isCurrentCharAlphaOrUnderscore: int;

    startIndex := charPointer;

    lex_is_alphanumeric_or_underscore(input[charPointer], isCurrentCharAlphaOrUnderscore);

    while (isCurrentCharAlphaOrUnderscore # 0) {
        NEXT_CHAR
        lex_is_alphanumeric_or_underscore(input[charPointer], isCurrentCharAlphaOrUnderscore); 
    }

    tokens[tokenPointer] := charPointer - startIndex;
    tokenPointer := tokenPointer + 1;
    array_cpy(input, tokens, startIndex, charPointer, tokenPointer);
    tokenPointer := tokenPointer + (charPointer - startIndex);

    charPointer := charPointer - 1;
}

proc lex_read_char_lit(ref input: DataArray, ref charPointer: int, ref tokens: DataArray, ref tokenPointer: int) {
    var readChar: int;

    NEXT_CHAR // Skip first single quote

    // 0x5C = backslash
    if (input[charPointer] = 0x5C) { 
        if (input[charPointer + 1] = 'n') {
            NEXT_CHAR
            readChar := '\n';
        } else {
            exit(); // LEXER ERROR: Expected escape sequence
        }
    } else {
        readChar := input[charPointer];
    }

    NEXT_CHAR
    if (input[charPointer] # 0x27) { // 0x27 = ' (single quote)
        exit(); // LEXER ERROR: Expected closing single quote
    }

    tokens[tokenPointer] := readChar;
    tokenPointer := tokenPointer + 1;
}

proc lex_read_decimal_int(ref input: DataArray, ref charPointer: int, ref tokens: DataArray, ref tokenPointer: int) {
    var parsed: int;
    var isDigit: int;

    parsed := CHAR_TO_INT(input[charPointer]);
    
    NEXT_CHAR
    lex_is_digit(input[charPointer], isDigit);

    while (isDigit # 0) {
        parsed := parsed * 10 + CHAR_TO_INT(input[charPointer]);
        NEXT_CHAR
        lex_is_digit(input[charPointer], isDigit); 
    }

    tokens[tokenPointer] := parsed;
    tokenPointer := tokenPointer + 1;
}

proc lex_read_hex_int(ref input: DataArray, ref charPointer: int, ref tokens: DataArray, ref tokenPointer: int) {
    var parsed: int;
    var decimal: int;

    parsed := 0;

    // Skip 0x
    NEXT_CHAR
    NEXT_CHAR

    lex_hex_to_int(input[charPointer], decimal);

    if (decimal = -1) {
        exit(); // LEXER ERROR: Expected number literal
    }

    while (decimal # -1) {
        parsed := parsed * 16 + decimal;
        NEXT_CHAR
        lex_hex_to_int(input[charPointer], decimal);
    }

    tokens[tokenPointer] := parsed;
    tokenPointer := tokenPointer + 1;
}


proc lex_match_keyword(ref input: DataArray, ref charPointer: int, ref keyword: int) {
    keyword := -1;
    
    K_MATCH(0, 'i') K_MATCH(1, 'f') {   
        keyword := TOK_IF;
        charPointer := charPointer + 2;
    }

    K_MATCH(0, 'e') K_MATCH(1, 'l') K_MATCH(2, 's') K_MATCH(3, 'e') {
        charPointer := charPointer + 4;
        keyword := TOK_ELSE;
    }

    K_MATCH(0, 'w') K_MATCH(1, 'h') K_MATCH(2, 'i') K_MATCH(3, 'l') K_MATCH(4, 'e') {
        charPointer := charPointer + 5;
        keyword := TOK_WHILE;
    }

    K_MATCH(0, 'a') K_MATCH(1, 'r') K_MATCH(2, 'r') K_MATCH(3, 'a') K_MATCH(4, 'y') {
        charPointer := charPointer + 5;
        keyword := TOK_ARRAY;
    }

    K_MATCH(0, 'o') K_MATCH(1, 'f') {
        charPointer := charPointer + 2;
        keyword := TOK_OF;
    }

    K_MATCH(0, 'p') K_MATCH(1, 'r') K_MATCH(2, 'o') K_MATCH(3, 'c') {
        charPointer := charPointer + 4;
        keyword := TOK_PROC;
    }

    K_MATCH(0, 'r') K_MATCH(1, 'e') K_MATCH(2, 'f') {
        charPointer := charPointer + 3;
        keyword := TOK_REF;
    }

    K_MATCH(0, 't') K_MATCH(1, 'y') K_MATCH(2, 'p') K_MATCH(3, 'e') {
        charPointer := charPointer + 4;
        keyword := TOK_TYPE;
    }

    K_MATCH(0, 'v') K_MATCH(1, 'a') K_MATCH(2, 'r') {
        charPointer := charPointer + 3;
        keyword := TOK_VAR;
    }
}

// TODO: Hex numbers
proc lex(ref tokens: DataArray, ref tokenPointer: int, ref input: DataArray, inputLength: int) {
    var charPointer: int;
    var currentChar: int;
    var peekChar: int;
    var isDigit: int;
    var isAlphaOrUnderscore: int;
    var matchedKeyword: int;

    charPointer := 0;
    tokenPointer := 0;

    while (charPointer < inputLength) {
        lex_eat_whitespace(input, charPointer);
        lex_eat_comment(input, charPointer);

        currentChar := input[charPointer];
        peekChar := input[charPointer + 1];

        lex_is_digit(currentChar, isDigit);
        lex_is_alphanumeric_or_underscore(currentChar, isAlphaOrUnderscore);
        
        if (currentChar = ';') {
            TOKEN(TOK_SEMIC)
        } else if (currentChar = ',') {
            TOKEN(TOK_COMMA)
        } else if (currentChar = '(') {
            TOKEN(TOK_LPAREN)
        } else if (currentChar = ')') {
            TOKEN(TOK_RPAREN)
        } else if (currentChar = '{') {
            TOKEN(TOK_LCURL)
        } else if (currentChar = '}') {
            TOKEN(TOK_RCURL)
        } else if (currentChar = '[') {
            TOKEN(TOK_LBRACK)
        } else if (currentChar = ']') {
            TOKEN(TOK_RBRACK)
        } else if (currentChar = '+') {
            TOKEN(TOK_PLUS)
        } else if (currentChar = '-') {
            TOKEN(TOK_MINUS)
        } else if (currentChar = '*') {
            TOKEN(TOK_ASTERISK)
        } else if (currentChar = '/') {
            TOKEN(TOK_SLASH)
        } else if (currentChar = '=') {
            TOKEN(TOK_EQUAL)
        } else if (currentChar = '#') {
            TOKEN(TOK_HASH)
        } else if (currentChar = ':') {
            if (peekChar = '=') {
                NEXT_CHAR
                TOKEN(TOK_ASSGN)
            } else {
                TOKEN(TOK_COLON)
            }
        } else if (currentChar = '<') {
            if (peekChar = '=') {
                NEXT_CHAR
                TOKEN(TOK_LTE)
            } else {
                TOKEN(TOK_LT)
            }
        } else if (currentChar = '>') {
            if (peekChar = '=') {
                NEXT_CHAR
                TOKEN(TOK_GTE)
            } else {
                TOKEN(TOK_GT)
            }
        } else if (currentChar = 0x27) { // 0x27 = ' (single quote)
            TOKEN(TOK_CHAR)
            lex_read_char_lit(input, charPointer, tokens, tokenPointer);
        } else if (isDigit # 0) {
            TOKEN(TOK_INT)
            if (currentChar = '0') {
                if (peekChar = 'x') {
                    lex_read_hex_int(input, charPointer, tokens, tokenPointer);
                } else {
                    lex_read_decimal_int(input, charPointer, tokens, tokenPointer);
                }
            } else {
                lex_read_decimal_int(input, charPointer, tokens, tokenPointer);
            }
        } else if (isAlphaOrUnderscore # 0) {
            lex_match_keyword(input, charPointer, matchedKeyword);

            if (matchedKeyword # -1) {
                TOKEN(matchedKeyword)
            } else {
                TOKEN(TOK_IDENT)
                lex_read_ident(input, charPointer, tokens, tokenPointer);
            }
        } else {
            TOKEN(TOK_ILLEGAL)
        }

        NEXT_CHAR
    }

    TOKEN(TOK_EOF)
}

proc print_tokens(ref tokens: DataArray, tokenPointer: int) {
    var head: int;
    var i: int;
    var t: int;

    i := 0;

    while (i < tokenPointer) {
        head := tokens[i];

        if (head = TOK_IDENT) {
            printc('i'); printc('d'); printc('e'); printc('n'); printc('t'); printc(' ');
            i := i + 1;
            t := i + tokens[i];
            while (i < t) {
                printc(tokens[i + 1]);
                i := i + 1;
            }
        } else if (head = TOK_INT) {
            printc('i'); printc('n'); printc('t'); printc(' ');
            printi(tokens[i + 1]);
            i := i + 1;
        } else if (head = TOK_CHAR) {
            printc('c'); printc('h'); printc('a'); printc('r'); printc(' ');
            printc(tokens[i + 1]);
            i := i + 1;
        } else if (head = TOK_IF) {
            printc('i'); printc('f');
        } else if (head = TOK_ELSE) {
            printc('e'); printc('l'); printc('s'); printc('e');
        } else if (head = TOK_WHILE) {
            printc('w'); printc('h'); printc('i'); printc('l'); printc('e');
        } else if (head = TOK_ARRAY) {
            printc('a'); printc('r'); printc('r'); printc('a'); printc('y');
        } else if (head = TOK_OF) {
            printc('a'); printc('o'); printc('f');
        } else if (head = TOK_PROC) {
            printc('p'); printc('r'); printc('o'); printc('c');
        } else if (head = TOK_REF) {
            printc('r'); printc('e'); printc('f');;
        } else if (head = TOK_TYPE) {
            printc('t'); printc('y'); printc('p'); printc('e');
        } else if (head = TOK_VAR) {
            printc('v'); printc('a'); printc('r');
        } else if (head = TOK_LPAREN) {
            printc('(');
        } else if (head = TOK_RPAREN) {
            printc(')');
        } else if (head = TOK_LBRACK) {
            printc('[');
        } else if (head = TOK_RBRACK) {
            printc(']');
        } else if (head = TOK_LCURL) {
            printc('{');
        } else if (head = TOK_RCURL) {
            printc('}');
        } else if (head = TOK_EQUAL) {
            printc('=');
        } else if (head = TOK_HASH) {
            printc('#');
        } else if (head = TOK_LT) {
            printc('<');
        } else if (head = TOK_LTE) {
            printc('<'); printc('=');
        } else if (head = TOK_GT) {
            printc('>');
        } else if (head = TOK_GTE) {
            printc('>'); printc('=');
        } else if (head = TOK_PLUS) {
            printc('+');
        } else if (head = TOK_MINUS) {
            printc('-');
        } else if (head = TOK_ASTERISK) {
            printc('*');
        } else if (head = TOK_SLASH) {
            printc('/');
        } else if (head = TOK_COLON) {
            printc(':');
        } else if (head = TOK_SEMIC) {
            printc(';');
        } else if (head = TOK_COMMA) {
            printc(',');
        } else if (head = TOK_ASSGN) {
            printc(':'); printc('=');
        } else if (head = TOK_EOF) {
            printc('E'); printc('O'); printc('F');
        } else if (head = TOK_ILLEGAL) {
            printc('I'); printc('l'); printc('l'); printc('e'); printc('g'); printc('a'); printc('l');
        }

        printc('\n');

        i := i + 1;
    }
}

proc main() {
    var i: int;
    var input: DataArray;
    var inputLength: int;
    var tokenPointer: int;
    var tokens: DataArray;

    tokenPointer := 0;

    read_input(input, inputLength);

    lex(tokens, tokenPointer, input, inputLength);

    print_tokens(tokens, tokenPointer);
}